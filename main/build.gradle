plugins {
    id 'java'
    id 'application'
}

group 'com.agustibm'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.10

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':loadmodules')

    runtimeOnly project(':user')
    runtimeOnly project(':pet')

    compile 'com.google.inject:guice:4.2.0'
    compile 'io.javalin:javalin:2.0.0'
    compile 'org.slf4j:slf4j-simple:1.7.25'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

import java.util.regex.Matcher

ext.moduleName  = 'com.agustibm.javalin.jigsaw.main'

mainClassName = "$moduleName/com.agustibm.javalin.jigsaw.main.App"

run {
    inputs.property("moduleName", moduleName)
    doFirst {
        jvmArgs = [
                '--module-path', classpath.asPath,
                '--module', mainClassName // <2>
        ]
        classpath = files()
    }
}

startScripts {
    inputs.property("moduleName", moduleName)
    doFirst {
        classpath = files()
        defaultJvmOpts = [
                '--module-path', 'APP_HOME_LIBS',  // <1>
                '--module', mainClassName
        ]
    }
    doLast{
        def bashFile = new File(outputDir, applicationName)
        String bashContent = bashFile.text
        bashFile.text = bashContent.replaceFirst('APP_HOME_LIBS', Matcher.quoteReplacement('$APP_HOME/lib'))

        def batFile = new File(outputDir, applicationName + ".bat")
        String batContent = batFile.text
        batFile.text = batContent.replaceFirst('APP_HOME_LIBS', Matcher.quoteReplacement('%APP_HOME%\\lib'))
    }
}

